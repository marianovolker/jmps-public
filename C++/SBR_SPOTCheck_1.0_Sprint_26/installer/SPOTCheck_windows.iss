; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!


[Setup]
AppName=SPOTCheck
AppVerName=SPOTCheck 02.00.26
AppPublisher=Gilbarco Inc.
DefaultDirName={pf}\Gilbarco\SPOTCheck
DefaultGroupName=SPOTCheck
AllowNoIcons=yes
OutputDir=.\target
OutputBaseFilename=SPOTCheck_installer_02_00_26
Compression=lzma
SolidCompression=yes
UserInfoPage=yes
LicenseFile=SPOTCheck_EULA.rtf

[Languages]
Name: "english"; MessagesFile: "compiler:default.isl"

[Files]
Source: "..\.build_windows\app\spotcheck\spotcheck.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\.build_windows\app\spotdashboard\spotdashboard.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\installer\README.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\installer\SPOTCheck.ini"; DestDir: "{app}"; Permissions: users-modify; Flags: ignoreversion
Source: "..\installer\SPOTDashboard.ini"; DestDir: "{app}"; Permissions: users-modify; Flags: ignoreversion
Source: "..\installer\resources\*"; Excludes: ".svn"; DestDir: "{app}\resources\"; Flags: recursesubdirs
Source: "..\installer\images\*"; Excludes: ".svn"; DestDir: "{app}\images\"; Flags: recursesubdirs
Source: "Z:\usr\local\wxpack-w32\dll\wxbase28_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\wxbase28_xml_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\wxmsw28_aui_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\wxmsw28_core_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\wxmsw28_html_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\wxmsw28_richtext_gcc.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\libgcc_s_sjlj-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\libstdc++-6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\usr\local\wxpack-w32\dll\mingwm10.dll"; DestDir: "{app}"; Flags: ignoreversion


; these items will be installed only when the IsFieldInstallation
; function returns True, what will depend on the selected radio button on installer screen.
Source: "..\installer\field_config\SPOTDashboard.ini"; DestDir: "{app}"; Permissions: users-modify; Flags: ignoreversion; Check: IsFieldInstallation;
Source: "..\installer\field_config\SPOTCheck.ini"; DestDir: "{app}"; Permissions: users-modify; Flags: ignoreversion; Check: IsFieldInstallation;


; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: "DestDir"; Flags: uninsalwaysuninstall

[Icons]
Name: "{group}\SPOTCheck"; Filename: "{app}\spotcheck.exe"; WorkingDir: "{app}"
Name: "{group}\SPOTCheck config"; Filename: "{app}\SPOTCheck.ini"; WorkingDir: "{app}"
Name: "{group}\SPOTDash"; Filename: "{app}\spotdashboard.exe"; WorkingDir: "{app}"
Name: "{group}\SPOTDash config"; Filename: "{app}\SPOTDashboard.ini"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallProgram,SPOTCheck}"; Filename: "{uninstallexe}"; WorkingDir: "{app}"

[Registry]
Root: HKLM; Subkey: "SOFTWARE\GILBARCO\"; Flags: uninsdeletekey
Root: HKLM; Subkey: "SOFTWARE\GILBARCO\SPOTCheck"; Flags: uninsdeletekey
Root: HKLM; Subkey: "SOFTWARE\GILBARCO\SPOTCheck\Settings"; Flags: uninsdeletekey
Root: HKLM; Subkey: "SOFTWARE\GILBARCO\SPOTCheck\Settings"; ValueType: string; ValueName: "APPL_ID"; ValueData: "129"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: String; ValueName: "{app}\spotcheck.exe"; ValueData: "WIN98"; Flags: Uninsdeletekey
Root: HKCU; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: String; ValueName: "{app}\spotcheck.exe"; ValueData: "RUNASADMIN"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: String; ValueName: "{app}\spotdashboard.exe"; ValueData: "WIN98"; Flags: Uninsdeletekey
Root: HKCU; Subkey: "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers"; ValueType: String; ValueName: "{app}\spotdashboard.exe"; ValueData: "RUNASADMIN"; Flags: uninsdeletekey


; author: slipchak
; code to show a dialog in the installation process to select factory or field type of installation see above configuration for different files copied in that case

[Code]
const
  FullDescText =
    'Field installation. Will prompt the SPOT ip';
  PartDescText =
    'Factory installation. Will use a default factory ip ( i.e. 10.5.48.71) ';

var
  FullRadioButton: TNewRadioButton;
  PartRadioButton: TNewRadioButton;

procedure InitializeWizard;
var
  CustomPage: TWizardPage;
  FullDescLabel: TLabel;
  PartDescLabel: TLabel;
begin
  WizardForm.LicenseAcceptedRadio.Checked := True;
  CustomPage := CreateCustomPage(wpWelcome, 'Please select Installation type', '');
  FullRadioButton := TNewRadioButton.Create(WizardForm);
  FullRadioButton.Parent := CustomPage.Surface;
  FullRadioButton.Checked := True;
  FullRadioButton.Top := 16;
  FullRadioButton.Width := CustomPage.SurfaceWidth;
  FullRadioButton.Font.Style := [fsBold];
  FullRadioButton.Font.Size := 9;
  FullRadioButton.Caption := 'Field Installation'
  FullDescLabel := TLabel.Create(WizardForm);
  FullDescLabel.Parent := CustomPage.Surface;
  FullDescLabel.Left := 8;
  FullDescLabel.Top := FullRadioButton.Top + FullRadioButton.Height + 8;
  FullDescLabel.Width := CustomPage.SurfaceWidth; 
  FullDescLabel.Height := 40;
  FullDescLabel.AutoSize := False;
  FullDescLabel.Wordwrap := True;
  FullDescLabel.Caption := FullDescText;
  PartRadioButton := TNewRadioButton.Create(WizardForm);
  PartRadioButton.Parent := CustomPage.Surface;
  PartRadioButton.Top := FullDescLabel.Top + FullDescLabel.Height + 16;
  PartRadioButton.Width := CustomPage.SurfaceWidth;
  PartRadioButton.Font.Style := [fsBold];
  PartRadioButton.Font.Size := 9;
  PartRadioButton.Caption := 'Factory Installation'
  PartDescLabel := TLabel.Create(WizardForm);
  PartDescLabel.Parent := CustomPage.Surface;
  PartDescLabel.Left := 8;
  PartDescLabel.Top := PartRadioButton.Top + PartRadioButton.Height + 8;
  PartDescLabel.Width := CustomPage.SurfaceWidth;
  PartDescLabel.Height := 40;
  PartDescLabel.AutoSize := False;
  PartDescLabel.Wordwrap := True;
  PartDescLabel.Caption := PartDescText;
end;


function IsFieldInstallation: Boolean;
begin
  Result := FullRadioButton.Checked;
end;
